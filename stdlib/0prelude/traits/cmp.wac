# Comparison traits

##############################################################################
# Eq
#
# Check two values for equivalence
# The '==' and '!=' operators use this function
#
##############################################################################
trait Eq(self, other id) bool

impl bool for Eq(self, other id) bool {
    self is other
}

impl i32 for Eq(self, other id) bool {
    self is other
}

impl f32 for Eq(self, other id) bool {
    self is other
}

impl str for Eq(self, other id) bool {
    self is other or type(other) is str and {
        var other str = other
        __str_eq(self, other)
    }
}

impl list for Eq(self, other id) bool {
    self is other or type(other) is list and {
        var other list = other
        __list_eq(self, other)
    }
}

##############################################################################
# LessThan
#
# Compares two values
#
##############################################################################
trait LessThan(self, other id) bool

fn GreaterThan(self id, other id) bool {
    LessThan(other, self)
}

fn LessThanOrEqual(self id, other id) bool {
    !LessThan(other, self)
}

fn GreaterThanOrEqual(self id, other id) bool {
    !LessThan(self, other)
}

impl str for LessThan(self, other id) bool {
    self.LessThan(other)
}
